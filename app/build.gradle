/*
 * Copyright (c) 2015 Markus Poeschl
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {

    dependencies {
        classpath 'com.github.jcandksolutions.gradle:android-unit-test:2.0.2'
        classpath 'io.fabric.tools:gradle:1.14.4'
        classpath 'de.undercouch:gradle-download-task:1.2'
        classpath 'com.github.triplet.gradle:play-publisher:1.0.4'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 1

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
project.ext.set("gitSha", gitSha)

android {

    compileSdkVersion 21
    buildToolsVersion "22"

    signingConfigs {
        googlePlay {
            //Placeholder which gets automatically filled during project sync. Needs the right setting file.
            //This settings is not in the source control, because it contains sensitive information.
        }
    }

    defaultConfig {
        applicationId "de.poeschl.apps.tryandremove"
        minSdkVersion 15
        targetSdkVersion 22

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        //Setup the fabric keys
        def properties = getExternalProperties()
        resValue "string", "fabric_key", properties["crashlytics_apiKey"]
        resValue "string", "fabric_appSecret", properties["crashlytics_appSecret"]
        println "Setup fabric api keys"
    }

    lintOptions {
        lintConfig file("lint.xml")
        checkReleaseBuilds true
        abortOnError true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.googlePlay

            archivesBaseName = "Try And Remove - " + defaultConfig.versionName + " - " + project.gitSha
        }
        debug {
            applicationIdSuffix '.dev'
            ext.enableCrashlytics = false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

apply plugin: 'android-unit-test'

androidUnitTest {
    testReleaseBuildType true
    //Run tests for all the build types including non-debuggable (like the Release build type). Only works for Application projects, not Library projects.
    downloadDependenciesSources false
    //Download the sources.jar for the production dependencies. `true` by default.
    downloadDependenciesJavadoc true
    //Download the javadoc.jar for the production dependencies. `false` by default.
    downloadTestDependenciesSources false
    //Download the sources.jar for the test dependencies. `true` by default.
    downloadTestDependenciesJavadoc true
    //Download the javadoc.jar for the test dependencies. `false` by default.
}

apply plugin: 'jacoco-android'
jacocoAndroidUnitTestReport {
    excludes += ['**/*$ViewBinder*.*',
                 '**/*_MembersInjector*.*',
                 '**/*_Factory*.*']
    print excludes
}

apply plugin: 'play'
play {
    def properties = getExternalProperties()
    serviceAccountEmail = properties["serviceAccountMail"]
    pk12File = file(properties["path"] + '/' + properties["serviceAccountKey"])

    track = 'production'

    uploadImages = true

    println("Setup Play Store plugin")
}

apply plugin: 'checkstyle'

checkstyle {
    configFile new File("${project.buildDir}/checkstyle.xml")
    reportsDir new File("${project.buildDir}/outputs")
    toolVersion "6.2"
}

//Add checkstyle execution
preBuild.dependsOn('checkstyle')

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/R.java'
    exclude '**/BuildConfig.java'

    def configProps = ['proj.module.dir': buildDir]
    configProperties configProps

    //If you are offline and the checkstyle stuff was downloaded once you can comment the next line out
    it.dependsOn 'downloadCheckstyleConfig'
    classpath = files()
}
apply plugin: 'de.undercouch.download'
import de.undercouch.gradle.tasks.download.Download

task downloadCheckstyleConfig(type: Download) {
    src([
            'https://raw.githubusercontent.com/Poeschl/Android-Checkstyle/master/checkstyle.xml',
            'https://raw.githubusercontent.com/Poeschl/Android-Checkstyle/master/suppressions.xml'
    ])
    dest buildDir
    overwrite true
    onlyIfNewer true
}

def getExternalProperties() {
    //Get environment variable of keystore settings folder location.
    def keyStorePath = System.getenv('Android_Keystore_Path')

    //Name of the properties file
    def propertiesFileName = 'TryAndRemove.keyproperties'

    def propsFile = file(keyStorePath + '/' + propertiesFileName)

    def properties;
    if (propsFile.exists()) {
        properties = new Properties()
        properties.load(new FileInputStream(propsFile))
        properties.put('path', keyStorePath)

    } else {
        if (System.getenv('CI')) {
            println("Travis detected, using Environment variables for fabric");
            properties = new Properties()
            properties.put("crashlytics_appSecret", System.getenv('crashlytics_appSecret'))
            properties.put("crashlytics_apiKey", System.getenv('crashlytics_apiKey'))
        } else {
            System.err.println("File " + propsFile.name + " for setup not available");
            //To let the project at least init
            properties = new Properties()
            properties.put("crashlytics_appSecret", "123")
            properties.put("crashlytics_apiKey", "123")
        }
    }

    return properties
}

task setupSigning() {
    def props = getExternalProperties();

    setupSigning(props)
    setupFabricApiFile(props)
}

/*
 * Adds dynamically keys and secret stuff to the project
 */
afterEvaluate { project ->
//    project.tasks.findByName("generateReleasePlayResources")
    def generateReleasePlayResources = project.tasks.findByName("generateReleasePlayResources")
    if (generateReleasePlayResources != null) {
        println "Play sources generation not available"
        generateReleasePlayResources.dependsOn setupSigning
    }
}

def setupSigning(properties) {
    if (properties == null && properties["storePassword"] != null) {
        System.err.println("Could not set signing config. No properties");
        return;
    }

    def configName = 'googlePlay'

    if (android.signingConfigs.hasProperty(configName)) {

        //Android signing information
        android.signingConfigs[configName].storeFile = file(properties['path'] + '/' + properties['storeFile'])
        android.signingConfigs[configName].storePassword = properties['storePassword']
        android.signingConfigs[configName].keyAlias = properties['keyAlias']
        android.signingConfigs[configName].keyPassword = properties['keyPassword']
        println("Setup keystore settings.")
    }
}

def setupFabricApiFile(properties) {

    File crashlyticsProps = new File(getBuildFile().getPath().replace(getBuildFile().getName(), "crashlytics.properties"));
    FileWriter writer = new FileWriter(crashlyticsProps, false);

    try {
        writer.write("apiSecret=" + properties["crashlytics_appSecret"]);
        writer.write("\n")
        writer.write("apiKey=" + properties["crashlytics_apiKey"]);
    } finally {
        writer.close();
    }
    println("Setup crashlytics key file.")
}

dependencies {
    compile 'com.android.support:recyclerview-v7:21.0.3'
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:palette-v7:22.0.0'
    compile 'com.google.android.gms:play-services-ads:7.0.0'

    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jakewharton.timber:timber:2.7.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.makeramen:roundedimageview:2.0.0'
    compile "com.daimajia.swipelayout:library:1.1.8@aar"
    //    Is replaced with local aar until changes gets merged
    //    compile 'com.getbase:floatingactionbutton:1.4.0'
    compile project(':android-floating-action-button')

    compile('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') {
        transitive = true;
    }

    testCompile 'junit:junit:4.10'
    testCompile 'org.robolectric:robolectric:2.4'

    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.3.1'
    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'

    debugCompile 'com.android.support:support-v4:22.0.0'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'
}
